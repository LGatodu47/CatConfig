plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.lgatodu47'
archivesBaseName = project.lib_name
version project.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains:annotations:${annotations_version}"
    implementation "com.google.code.gson:gson:${gson_version}"
    implementation "it.unimi.dsi:fastutil:${fastutil_version}"
    implementation "com.google.guava:guava:${guava_version}"

    compileOnly 'org.apache.logging.log4j:log4j-api:2.19.0'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.19.0'
}

jar {
    libsDirName = "libs/${version}"
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        catconfig(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'CatConfig'
                description = 'Config File handling library'
                url = 'https://github.com/LGatodu47/CatConfig'
                licenses {
                    license {
                        name = 'MIT License'
                        url = "https://www.opensource.org/licenses/mit-license.php"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/LGatodu47/CatConfig.git"
                    developerConnection = "scm:git:ssh://github.com/LGatodu47/CatConfig.git"
                    url = "https://github.com/LGatodu47/CatConfig"
                }

                developers {
                    developer {
                        id = 'lgatodu47'
                        name = 'LGatodu47'
                        email = 'driph.v450@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "Local"
            url layout.buildDirectory.dir("repo")
        }
        maven {
            name = "OSSRH"
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'

            credentials {
                username System.getProperty("catconfig.publish.username")
                password System.getProperty("catconfig.publish.password")
            }
        }
    }
}

signing {
    sign publishing.publications.catconfig
}
